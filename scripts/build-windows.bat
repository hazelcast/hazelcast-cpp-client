@SET HZ_BIT_VERSION=%1
@SET HZ_LIB_TYPE=%2
@SET HZ_BUILD_TYPE=%3
@SET COMPILE_WITHOUT_SSL=%4

@SET BUILD_DIR=build%HZ_LIB_TYPE%%HZ_BIT_VERSION%%HZ_BUILD_TYPE%

@SET EXECUTABLE_NAME=clientTest_%HZ_LIB_TYPE%_%HZ_BIT_VERSION%.exe

@echo HZ_BIT_VERSION=%HZ_BIT_VERSION%
@echo HZ_LIB_TYPE=%HZ_LIB_TYPE%
@echo HZ_BUILD_TYPE=%HZ_BUILD_TYPE%
@echo BUILD_DIR=%BUILD_DIR%
@echo EXECUTABLE_NAME=%EXECUTABLE_NAME%

set SOLUTIONTYPE="Visual Studio 16 2019"

if %HZ_BIT_VERSION% == 32 (
    set BUILDFORPLATFORM="win32"
    @SET OPENSSL_ROOT_DIR=C:\OpenSSL-Win32
) else (
    set BUILDFORPLATFORM="x64"
    @SET OPENSSL_ROOT_DIR=C:\OpenSSL-Win64
)

if "%COMPILE_WITHOUT_SSL%" == "COMPILE_WITHOUT_SSL" (
    set HZ_COMPILE_WITH_SSL=OFF
) else (
    set HZ_COMPILE_WITH_SSL=ON
)

RD /S /Q %BUILD_DIR%
mkdir %BUILD_DIR%

pushd %BUILD_DIR%

echo "Generating the solution files for compilation"
cmake .. -G %SOLUTIONTYPE% -A %BUILDFORPLATFORM% -DHZ_LIB_TYPE=%HZ_LIB_TYPE% -DHZ_BIT=%HZ_BIT_VERSION% -DCMAKE_BUILD_TYPE=%HZ_BUILD_TYPE% -DHZ_BUILD_TESTS=ON -DBUILD_GMOCK=OFF -DHZ_BUILD_EXAMPLES=ON -DOPENSSL_ROOT_DIR=%OPENSSL_ROOT_DIR% -DHZ_COMPILE_WITH_SSL=%HZ_COMPILE_WITH_SSL% -DBUILD_GMOCK=OFF -DINSTALL_GTEST=OFF || exit /b 1
MSBuild.exe HazelcastClient.sln /m /p:Flavor=%HZ_BUILD_TYPE%;Configuration=%HZ_BUILD_TYPE%;Platform=%BUILDFORPLATFORM%;PlatformTarget=%BUILDFORPLATFORM% /verbosity:n || exit /b 1

popd

exit /b 0
